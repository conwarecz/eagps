buildscript {
	repositories {
		maven { url 'https://maven.fabric.io/public' }
	}

	dependencies {
		classpath 'io.fabric.tools:gradle:1.25.1'
	}
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
	maven { url 'https://maven.fabric.io/public' }
}

apply plugin: 'realm-android'

android {
	compileSdkVersion 27
	defaultConfig {
		applicationId "cz.europassistance.assistancepartners"
		minSdkVersion 16
		targetSdkVersion 27
		versionCode 55
		versionName "0.5.11"
		multiDexEnabled true
		testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
		compileOptions {
			sourceCompatibility JavaVersion.VERSION_1_8
			targetCompatibility JavaVersion.VERSION_1_8
		}
		javaCompileOptions {
			annotationProcessorOptions {
				// Needs to be same as package name and manifest package
				arguments = ['resourcePackageName': 'net.aineuron.eagps']
			}
		}
	}
	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
	}
	flavorDimensions "default"
	productFlavors {
		flavtest {
			buildConfigField "String", "ENDPOINT_URL", '"https://www.vgsdapi-test.europ-assistance.cz:41443/"'
			versionNameSuffix "-test"
		}
		preprod {
			buildConfigField "String", "ENDPOINT_URL", '"https://www.vgsdapi-preprod.europ-assistance.cz:41443/"'
			versionNameSuffix "-preprod"
		}
		production {
			buildConfigField "String", "ENDPOINT_URL", '"https://www.eaprg.cz/"'
		}
	}
	android.applicationVariants.all { variant ->
		if (variant.name.endsWith("Debug"))
			return
		variant.outputs.all {
			outputFileName = "eagps-${variant.versionName}.apk"
		}
		variant.assemble.doLast { assemble ->
			//copy the apk in another directory, add here your
			//logic to customize the destination folder
			copy {
				from variant.outputs*.outputFile
				into "${project.projectDir}/release"
			}
			//if you don't want to delete the file after copying it comment the line below
			delete variant.outputs*.outputFile
		}
	}
	packagingOptions {
		exclude 'META-INF/LICENSE.txt'
		exclude 'META-INF/DEPENDENCIES'
		exclude 'META-INF/LICENSE'
		exclude 'META-INF/MANIFEST.MF'
		exclude 'META-INF/DEPENDENCIES.txt'
		exclude 'META-INF/NOTICE.txt'
		exclude 'META-INF/NOTICE'
		exclude 'META-INF/LGPL2.1'
	}
	lintOptions {
		checkReleaseBuilds false
	}
	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}

	dependencies {
		implementation fileTree(include: ['*.jar'], dir: 'libs')
		androidTestImplementation("com.android.support.test.espresso:espresso-core:2.2.2", {
			exclude group: "com.android.support", module: "support-annotations"
		})
		testImplementation "junit:junit:4.12"
		// Support libraries
		String supportVersion = "27.1.0"
		implementation "com.android.support:multidex:1.0.3"
		implementation "com.android.support:appcompat-v7:$supportVersion"
		implementation "com.android.support:cardview-v7:$supportVersion"
		implementation "com.android.support:recyclerview-v7:$supportVersion"
		implementation "com.android.support:design:$supportVersion"
		implementation "com.android.support.constraint:constraint-layout:1.0.2"
		// Client
		implementation "com.squareup.retrofit2:retrofit:2.3.0"
		implementation "com.squareup.retrofit2:converter-gson:2.3.0"
		implementation "com.squareup.retrofit2:adapter-rxjava2:2.3.0"
		implementation "com.squareup.okhttp3:okhttp:3.9.1"
		implementation "com.squareup.okhttp3:logging-interceptor:3.9.1"
		implementation "com.github.bumptech.glide:glide:3.8.0"
		// Architecture
		implementation "org.greenrobot:eventbus:3.0.0"
		implementation "io.reactivex.rxjava2:rxjava:2.1.8"
		implementation "io.reactivex.rxjava2:rxandroid:2.0.1"
		implementation "org.parceler:parceler-api:1.1.9"
		annotationProcessor "org.parceler:parceler:1.1.9"
		implementation "com.github.kobeumut:UpdateChecker:0.1.3"
		// View
		implementation "com.ashokvarma.android:bottom-navigation-bar:2.0.2"
		implementation "com.github.jetradarmobile:multibackstack:1.2.1"
		implementation "com.afollestad.material-dialogs:core:0.9.6.0"
		implementation "com.mobsandgeeks:android-saripaar:2.0.3"
		implementation "com.zhihu.android:matisse:0.4.3"
		implementation "io.realm:android-adapters:2.1.0"
		// Permissions
		implementation("com.github.hotchemi:permissionsdispatcher:3.1.0") {
			// if you don"t use android.app.Fragment you can exclude support for them
			exclude module: "support-v13"
		}
		annotationProcessor "com.github.hotchemi:permissionsdispatcher-processor:3.1.0"
		// Permission dispatcher annotation
		annotationProcessor "com.github.AleksanderMielczarek:AndroidAnnotationsPermissionsDispatcherPlugin:2.1.1"
		// AA
		annotationProcessor "org.androidannotations:androidannotations:4.4.0"
		implementation "org.androidannotations:androidannotations-api:4.4.0"
		// Android Annotations for Greenrobot
		annotationProcessor "com.tmtron:green-annotations:1.0.2"
		implementation "com.tmtron:green-annotations-api:1.0.2"
		//Notifications
		implementation "com.google.firebase:firebase-messaging:11.8.0"
		implementation("com.crashlytics.sdk.android:crashlytics:2.7.0@aar") {
			transitive = true
		}
	}
	apply plugin: 'com.google.gms.google-services'
}